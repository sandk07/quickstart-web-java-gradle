task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

project(':web') {
	apply plugin: 'java'
	apply plugin: 'war'
	apply plugin: 'sonar-runner'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'com.bmuschko.cargo'
	
	repositories {
		mavenLocal()
		//mavenCentral()
	}

	dependencies {
		compile group: 'commons-io', name: 'commons-io', version: '1.4'
		compile group: 'log4j', name: 'log4j', version: '1.2.15', ext: 'jar'
		
		providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
		compile 'org.springframework:spring-webmvc:4.2.0.RELEASE'
		runtime 'javax.servlet:jstl:1.1.2'
		
		compile 'org.seleniumhq.selenium:selenium-java:2.46.0'
		compile 'org.testng:testng:6.8'
        }

        sourceSets {
		integrationTest {
			java.srcDir file("src/test-integration/java")
			resources.srcDir file("src/test-integration/resources")
			compileClasspath = sourceSets.main.output + configurations.testRuntime
			runtimeClasspath = output + compileClasspath
		}
		performanceTest {
			java.srcDir file("src/test-performance/java")
			resources.srcDir file("src/test-performance/resources")
			compileClasspath = sourceSets.main.output + configurations.testRuntime
			runtimeClasspath = output + compileClasspath
		}
		functionalTest {
			java.srcDir file("src/test-functional/java")
			resources.srcDir file("src/test-functional/resources")
			compileClasspath = sourceSets.main.output + configurations.testRuntime
			runtimeClasspath = output + compileClasspath
		}
	}

	task createBuildInfoFile << {
		def buildInfoFile = new File("$buildDir/build-info.properties")
		Properties props = new Properties()
		props.setProperty('version', project.version.toString())
		props.setProperty('timestamp', project.buildTimestamp)
		props.store(buildInfoFile.newWriter(), null)
	}
	
	war {
		dependsOn createBuildInfoFile
		baseName = appName
		
		from(buildDir) {
			include 'build-info.properties'
			into('WEB-INF/classes')
		}
	}

	sonarRunner {
	    sonarProperties {
		property "sonar.host.url", sonarHostUrl
		property "sonar.jdbc.url", sonarJdbcUrl
		property "sonar.jdbc.driverClassName", sonarJdbcDriverClassName
		property "sonar.jdbc.username", sonarJdbcUserName
		property "sonar.jdbc.password", sonarJdbcPassword
	    }
	}

	subprojects {
		sonarRunner {
			sonarProperties {
				property "sonar.sourceEncoding", "UTF-8"
			}
		}
	}

	uploadArchives{
		repositories{
			mavenDeployer{
				repository(url: releasesRepoPath){
					authentication(userName:repoUserName, password:repoPassword)
				}
			}
		}
	}
	
	publishing {
		publications {
			webApp(MavenPublication) {
				groupId mavenGroupId
				artifactId appName
				version version
			
				from components.web
			}
		}
		repositories {
			maven {
				url releasesRepoPath
				credentials {
					username = repoUserName
					password = repoPassword
				}
			}
		}
	}
	
	repositories {
		maven {
			url releasesRepoPath 
		}
	}
	configurations {
		downloadWar
	}
	println "$mavenGroupId:$appName:$project.version@war"
	dependencies {		
		downloadWar ("$mavenGroupId:$appName:$project.version@war"){ 
			 artifact {
				name = "$appName"
				extension = 'war'
				type = 'war'
			}
    		}
	}

	task downloadBinaryArchive(type: Copy) {		
		from configurations.downloadWar
		into "$buildDir/libs"		
	}

	cargo {
	    containerId = config.server.containerId
	    port = config.server.port

	    deployable {
	    	def fileName = appName+"-"+version
	    	def filePath = "$buildDir/libs/$fileName"+".war"
		file = new File ("$buildDir/libs/$fileName"+".war")
		context = appName
	    }

	    local {
		homeDir = file(config.server.localHomeDir)
	    }
	}

	//Integration Test
	task integrationTest(type: Test) {
		description = 'Runs the integration tests.'
		group = 'verification'
		testClassesDir = sourceSets.integrationTest.output.classesDir
		classpath = sourceSets.integrationTest.runtimeClasspath
	}

	//Performance Test
	task performanceTest(type: Test) {
		description = 'Runs the performance tests.'
		group = 'verification'
		testClassesDir = sourceSets.performanceTest.output.classesDir
		classpath = sourceSets.performanceTest.runtimeClasspath
	}

	//Functional Test
	task functionalTest(type: Test) {
		testClassesDir = sourceSets.functionalTest.output.classesDir
		classpath = sourceSets.functionalTest.runtimeClasspath
	}
}


